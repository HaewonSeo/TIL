## 1. 학습 날짜
+ 2021-03-19(금)

## 2. 학습시간
+ 13:00 ~ 19:00 (자가)   
+ 20:00 ~ 24:00 (자가)
+ 총 학습시간 : 10시간

## 3. 학습 범위 및 주제
+ 아두이노 BTN, Buzzer

## 4. 동료 학습 방법


## 5. 학습 목표
+ 아두이노 BTN, Buzzer 동작

## 6. 상세 학습 내용
+ 실제 코딩에 소요한 시간 : 10시간    
    
### extern

다른 소스파일에 선언된 전역 변수 사용하고 싶을 때

```c
(a.c)
int num = 10;

(main.c)
extern int num;
// 이제 num 사용 가능!
```

[https://dojang.io/mod/page/view.php?id=802](https://dojang.io/mod/page/view.php?id=802)

### volatile

volatile 변수를 붙이면, 컴파일러는 해당 변수를 최적화에서 제외하여 항상 메모리에 접근하도록 만든다.

사용할 때마다 항상 메모리에 접근한다. 즉, 이 변수는 언제든지 값이 바뀔 수 있다는 의미이다.

[https://dojang.io/mod/page/view.php?id=749](https://dojang.io/mod/page/view.php?id=749)

최적화로 인해 발생할 수 있는 오류를 제거하기 위해 volatile을 사용한다.

최적화를 사용하지 않기 위한 방법으로 GCC 최적화 옵션을 -O0으로 주는 방법이 있다. 

### 사용자 정의 형

[https://blog.naver.com/justkukaro/80181766761](https://blog.naver.com/justkukaro/80181766761)

### 1. enum(열거형)

[https://dojang.io/mod/page/view.php?id=480](https://dojang.io/mod/page/view.php?id=480)

- 여기가 더 자세함 : [https://blog.naver.com/justkukaro/80181766761](https://blog.naver.com/justkukaro/80181766761)

enum 정의 후, enum 변수를 선언 없이도 사용 할 수 있다.

매크로처럼 사용가능

enum 내에 변수는 상수 or 변수로 사용가능

초기화해주지않으면 0부터 1씩 증가.

enum형 변수는 무조건 int형(4byte)

### 2. struct(구조체)

다른 형태의 자료를 다른 공간에 저장하는 사용자 정의 형

얕은 복사, 깊은 복사

### 3. Union(공용체)

다른 형태의 자료를 같은 공간에 저장하는 사용자 정의 형

### 4. typedef

열거형, 구조체, 공용체 모두 사용할 수 있다.

+ interrupt 방식으로 버튼 제어 
```
#include <MsTimer2.h>

int Button[2] = {2,3};            // Button Pin
int Led[3] = {5,6,7};             // LED Pin
static byte Led16_output = HIGH;  // LED 16의 On/Off를 결정하는 변수
 
// Timer function
void LedControl(){
   static int counter = 0;
   static byte output = HIGH;

   digitalWrite(Led[counter++], output);    // LED(1, 9) ON/OFF 수행

   if (counter > 1){
      counter = 0;
      output = !output;
   }
}

// ISR
void ButtonLedControl(){
  digitalWrite(Led[2], Led16_output);     // LED 16 On
}

void ButtonLedControl2(){
  digitalWrite(Led[2], !Led16_output);    // LED 16 Off
}

void setup() {
  int i;

  // 버튼 2개를 pull-up input 으로 설정
  for (i=0; i<2; i++)
    pinMode(Button[i], INPUT_PULLUP);

  // LED PIN 을 출력으로 설정
  for (i=0; i<3; i++)
    pinMode(Led[i], OUTPUT);

  // 인터럽트 설정
  attachInterrupt(0, ButtonLedControl, FALLING);
  attachInterrupt(1, ButtonLedControl2, RISING);

  // TIMER 설정
  MsTimer2::set(500, LedControl);

  // TIMER 시작
  MsTimer2::start();
}

void loop() {
}
```

+ debounce 고려하지 않고, polling 방식으로 버튼 제어
```
#include <MsTimer2.h>

int Button[2] = {2,3};            // Button Pin
int Led[3] = {5,6,7};             // LED Pin
static byte Led16_output = HIGH;  // LED 16의 On/Off를 결정하는 변수

static int buttonState[2] = {0, };      // 버튼의 현재 상태
static int lastButtonState[2] = {0, };  // 버튼의 이전 상태
static bool button0StateFalling = false;// button0의 falling edge 여부
static bool button1StateRising = false; // button1의 rising edge 여부


// Timer function
void LedControl(){
   static int counter = 0;
   static byte output = HIGH;

   digitalWrite(Led[counter++], output);    // LED(1, 9) ON/OFF 수행

   if (counter > 1){
      counter = 0;
      output = !output;
   }
}

void setup() {
  int i;

  // 버튼 2개를 pull-up input 으로 설정
  pinMode(Button[0], INPUT_PULLUP);
  pinMode(Button[1], INPUT_PULLUP);

  // LED PIN 을 출력으로 설정
  for (i=0; i<3; i++)
    pinMode(Led[i], OUTPUT);

  // TIMER 설정
  MsTimer2::set(500, LedControl);

  // TIMER 시작
  MsTimer2::start();
}


void loop() {
  int i;
  
  // 각 버튼의 현재 상태 read
  for (i=0; i<2; i++)
    buttonState[i] = digitalRead(Button[i]);

  // 버튼2의 상태가 변했고, 동시에 현재 상태가 0인 경우 falling edge
  if ((buttonState[0] != lastButtonState[0]) && (buttonState[0] == LOW))
    button0StateFalling = true;
  else
    button0StateFalling = false;

  // 버튼3의 상태가 변했고, 동시에 현재 상태가 1인 경우 rising edge
  if ((buttonState[1] != lastButtonState[1]) && (buttonState[1] == HIGH))
    button1StateRising = true;
  else
    button1StateRising = false;

  // LED16 On/Off
  if (button0StateFalling)
    digitalWrite(Led[2], Led16_output);   // LED16 On
  if (button1StateRising)
    digitalWrite(Led[2], !Led16_output);  // LED16 Off
  
  // 버튼의 이전 상태 저장
  for (i=0; i<2; i++)
    lastButtonState[i] = buttonState[i];
}

```

+ debounce를 고려하여 polling 방식으로 버튼 제어
```
#include <MsTimer2.h>

#define FALLING LOW
#define RISING  HIGH

int Button[2] = {2, 3};                       // Button Pin
int Led[3] = {5, 6, 7};                       // LED Pin
byte Led16Output;                             // LED16의 On/Off를 결정하는 변수
const int EdgeTrigger[2] = {FALLING, RISING}; // 버튼 edge trigger
const byte LedState[2] = {HIGH, LOW};         // 버튼 입력 시 수행할 동작
int ButtonState[2];                           // 버튼의 현재 상태, toggle 확인을 위해 사용
int LastButtonState[2];                       // 버튼의 이전 상태, debounce 확인을 위해 사용
unsigned long LastDebounceTime[2];            // 마지막으로 버튼의 입력 상태가 바뀐 시간
unsigned long DebounceDelay = 50;             // debounce time

// Timer function
void LedControl() {
  static int counter = 0;
  static byte output = HIGH;

  digitalWrite(Led[counter++], output);    // LED(1, 9) ON/OFF 수행

  if (counter > 1) {
    counter = 0;
    output = !output;
  }
}

// Debounce function
void Debounce(int buttonNum, int edgeTrigger, byte ledState) {
  int reading;

  reading = digitalRead(Button[buttonNum]);

  // 버튼의 입력상태가 변했을 경우 debounceTimer 초기화
  if (reading != LastButtonState[buttonNum])
    LastDebounceTime[buttonNum] = millis();

  // debounceTimer가 debounceDelay 이상일 경우, 버튼의 입력 변화로 인한 동작 수행
  if ((millis() - LastDebounceTime[buttonNum]) > DebounceDelay) {
    // 버튼의 상태가 toggle 되었다면, 버튼 상태 갱신 및 edgeTrigger 확인
    if (reading != ButtonState[buttonNum]) {
      ButtonState[buttonNum] = reading;
      // edgeTrigger 일치하면, Led16 출력값 설정
      if (ButtonState[buttonNum] == edgeTrigger)
        Led16Output = ledState;
    }
  }
  // LED16 On/Off
  digitalWrite(Led[2], Led16Output);
  // 버튼의 debounce를 확인하기 위해, 현재 입력 상태 저장
  LastButtonState[buttonNum] = reading;
}

void setup() {
  int i;

  // 버튼 2개를 pull-up input 으로 설정
  for (i = 0; i < 2; i++)
    pinMode(Button[i], INPUT_PULLUP);

  // LED PIN 을 출력으로 설정
  for (i = 0; i < 3; i++)
    pinMode(Led[i], OUTPUT);

  // TIMER 설정
  MsTimer2::set(500, LedControl);

  // TIMER 시작
  MsTimer2::start();
}

void loop() {
  int i;

  for (i = 0; i < 2; i++)
    Debounce(i, EdgeTrigger[i], LedState[i]);
}
```

## 7. 학습 내용에 대한 개인적인 총평
+ 수업에서 다룬 내용을 바탕으로 과제를 진행하였다.
+ Debounce를 고려하지 않고 버튼 제어를 진행하는 것은 쉬웠으나, Debounce를 고려하는 경우 까다로웠다.
+ 지금까지 학습하면서 참고한 예제들을 보면, 임베디드 프로그래밍에서 특히 중요한 것은 매크로와 사용자 정의 형을 자유롭게 사용하는 것이라 생각했다.
+ 아 그리고, 메모리 사용을 필요한 만큼만 사용하기 위해 자료형을 결정하는 것도 중요한 것 같다.
+ 그래서 이런 측면을 고려하여 코드를 작성하려고 노력하였다.

## 8. 다음 학습 계획
+ ARM 이론 학습

